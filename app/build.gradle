apply plugin: 'com.android.application'
apply from: rootProject.file('gradle/view-common.gradle')
apply plugin: 'io.fabric'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "jp.bellware.echo"
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("HOME") + "/keystores/echo.jks")
            storePassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PASSWORD")
            keyAlias System.getenv("BITRISEIO_ANDROID_KEYSTORE_ALIAS")
            keyPassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PRIVATE_KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        profile {
            initWith debug
        }
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

repositories {
    maven {
        url '../../sound_memo/build/host/outputs/repo/'
    }
    maven {
        url 'https://storage.googleapis.com/download.flutter.io'
    }
}

dependencies {
    implementation project(':main')
    implementation project(':setting')
    implementation project(':flux')
    testImplementation project(':test')

    debugImplementation 'jp.bellware.echo.sound_memo:flutter_debug:1.0'
    profileImplementation 'jp.bellware.echo.sound_memo:flutter_profile:1.0'
    releaseImplementation 'jp.bellware.echo.sound_memo:flutter_release:1.0'

    // Jetpack Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.nav_version"

    // Firebase
    // Debugビルドでは実行できないためReleaseビルドでしか使わない。
    // google-services.jsonがソースコード公開のためにモックになっているため。
    releaseImplementation "com.google.firebase:firebase-core:$firebase_core_version"
    releaseImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    releaseImplementation 'com.google.firebase:firebase-messaging:20.1.5'
    releaseImplementation 'com.google.firebase:firebase-dynamic-links:19.1.0'

    debugImplementation 'com.facebook.flipper:flipper:0.37.0'
    debugImplementation 'com.facebook.soloader:soloader:0.9.0'

    releaseImplementation 'com.facebook.flipper:flipper-noop:0.37.0'
}

apply plugin: 'com.google.gms.google-services'
