apply plugin: 'com.android.application'
apply plugin: 'deploygate'
apply from: rootProject.file('gradle/view-common.gradle')
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "jp.bellware.echo"
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("HOME") + "/keystores/echo.jks")
            storePassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PASSWORD")
            keyAlias System.getenv("BITRISEIO_ANDROID_KEYSTORE_ALIAS")
            keyPassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PRIVATE_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        profile {
            initWith debug
        }
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

}

dependencies {
    implementation project(':main')
    implementation project(':setting')
    implementation project(':memo')
    implementation project(':flux')
    implementation project(':common')
    // 直接参照しているモジュールしかHiltのモジュールを作れない
    implementation project(':repository')
    implementation project(':localDataStore')

    // Hiltのために、直接参照が必要。
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    // Firebase
    // Debugビルドでは実行できないためReleaseビルドでしか使わない。
    // google-services.jsonがソースコード公開のためにモックになっているため。
    releaseImplementation "com.google.firebase:firebase-core:$firebase_core_version"
    releaseImplementation 'com.google.firebase:firebase-messaging:20.1.5'
    releaseImplementation 'com.google.firebase:firebase-dynamic-links:19.1.0'

    debugImplementation 'com.facebook.flipper:flipper:0.37.0'
    debugImplementation 'com.facebook.soloader:soloader:0.9.0'

    releaseImplementation 'com.facebook.flipper:flipper-noop:0.98.0'
}

apply plugin: 'com.google.gms.google-services'
