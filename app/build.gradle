apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: "androidx.navigation.safeargs.kotlin"

def major = 1
def minor = 4
def patch = 0


android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "jp.bellware.echo"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode major * 10000 + minor * 100 + patch
        versionName "${major}.${minor}.${patch}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    signingConfigs {
        release {
            storeFile file(System.getenv("HOME") + "/keystores/echo.jks")
            storePassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PASSWORD")
            keyAlias System.getenv("BITRISEIO_ANDROID_KEYSTORE_ALIAS")
            keyPassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PRIVATE_KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

repositories {
    maven { url "https://jitpack.io" }
}

def lifecycle_version = "2.2.0"
def arch_version = "2.1.0"

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    // Architecture Components

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    testImplementation "androidx.arch.core:core-testing:$arch_version"
    // ConstraintLayout
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // KOIN
    def koin_version = '2.0.1'
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"

    // FluxのためにEventBusを使う
    implementation 'de.greenrobot:eventbus:2.4.1'

    // Kotlin Coroutine
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.2'

    // Jetpack Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.nav_version"

    // Firebase
    // Debugビルドでは実行できないためReleaseビルドでしか使わない。
    // google-services.jsonがソースコード公開のためにモックになっているため。
    releaseImplementation 'com.google.firebase:firebase-core:17.2.2'
    releaseImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    releaseImplementation 'com.google.firebase:firebase-messaging:20.1.0'
    releaseImplementation 'com.google.firebase:firebase-dynamic-links:19.1.0'

    // Preference
    implementation 'androidx.preference:preference:1.1.0'
    // Flow Preferences
    implementation 'com.github.tfcporciuncula:flow-preferences:1.0.0'

    // テスト
    testImplementation 'junit:junit:4.12'
    // coroutine test
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.3'
    // Kotlin Test
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.3.2'
    // MockK
    testImplementation "io.mockk:mockk:1.9.2"

    // インストルメンタル化テスト
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    
    // Module
    implementation project(':mymodule')
}

apply plugin: 'com.google.gms.google-services'
